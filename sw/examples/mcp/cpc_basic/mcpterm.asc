100 MODE 2 : OUT &FD81,0 : 'Reset Scn and FIFO
110 WINDOW #2,1,80,1,5 : CLS #2
120 WINDOW #0,1,80,8,24 : CLS #0
130 PRINT #2,"  CPC-CPlink/Pi MCP Terminal" 
140 PRINT #2: PRINT #2,"  Available commands:"  
150 PRINT #2,"  - DATE, TIME, PING, SHUTDOWN"
160 MOVE 10,396:DRAW 2,396:DRAW 2,320:DRAW 638,320:DRAW 638,396:DRAW 232,396
170 DEF FNdor=(INP(&FD81) AND 2)/2    
180 DEF FNdir=(INP(&FD81) AND 1)  
190 INPUT "MCP>",a$  : a$=a$+CHR$(&A)
200 IF ASC(LEFT$(A$,1))<33 THEN GOTO 190
210 FOR i=1 TO LEN(a$)
220   WHILE FNdor=0 : WEND
230   OUT &FD80,ASC( MID$(a$,i,1)) 
240 NEXT i
250 a$=""
260 WHILE RIGHT$(a$,1) <> CHR$(&A)
270   WHILE FNdir=0 : WEND 
280   a$=a$+CHR$(INP(&FD80))
290 WEND
300 PRINT a$;CHR$(&D);
310 GOTO 190
