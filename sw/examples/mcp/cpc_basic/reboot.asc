10 c$="REBOOT"
20 PRINT"Sending: ";c$
30 OUT &FD81,0 : 'Reset Scn and FIFO
40 DEF FNdir=(INP(&FD81) AND 2)/2    
50 DEF FNdor=(INP(&FD81) AND 1)  
60 c$=c$+CHR$(&A): 'command must be \n terminated
70 GOSUB 500: 'send the command as a packet
80 r$=""
90 GOSUB 600: 'read the response as a packet
100 print "Done."
110 END
500 'write the command as a packet to the pi
505 FOR i=1 TO 3
510   WHILE FNdir=0 : WEND
515   OUT &FD80,43: 'write the +++ starting delimiter
520 NEXT i
525 WHILE FNdir=0 : WEND
530 OUT &FD80,LEN(c$)+3: 'write the low byte of packet size
535 WHILE FNdir=0 : WEND
540 OUT &FD80,0: 'write the high byte of the paket size
545 WHILE FNdir=0 : WEND
550 OUT &FD80,1: 'write the command type (TextCommand = 1)
555 FOR i=1 TO LEN(c$)
560   WHILE FNdir=0 : WEND
565   OUT &FD80,ASC(MID$(c$,i,1)): 'write the message to the pi as the body of the packet
570 NEXT i
575 FOR i=1 TO 3
580   WHILE FNdir=0 : WEND
585   OUT &FD80,45: 'write the --- terminating delimiter
590 NEXT i
595 RETURN
600 'read the response from the pi
605 r$=""
610 FOR i=1 to 3
615   WHILE FNdor=0 : WEND 
620   r$=r$+CHR$(INP(&FD80)): 'read the +++ delimiter
625 NEXT i
630 IF r$ <> "+++" THEN GOTO 900
635 WHILE FNdor=0 : WEND
640 l=INP(&FD80): 'read the low byte of the packet size 
650 WHILE FNdor=0 : WEND
655 h=INP(&FD80): 'read the high byte of the packet size 
660 WHILE FNdor=0 : WEND
665 t=INP(&FD80): 'read the packet type (TextCommand=1) 
670 IF t <> 1 THEN GOTO 920
675 r$=""
676 '-3 for the packet overhead which we already read
680 FOR i=1 TO (((h * 256) + l) - 3)
685   WHILE FNdor=0 : WEND 
690   r$=r$+CHR$(INP(&FD80)): 'read the bytes of the message
695 NEXT i
700 PRINT r$: 'print the response payload
705 r$=""
710 FOR i=1 to 3
715   WHILE FNdor=0 : WEND 
720   r$=r$+CHR$(INP(&FD80)): 'read the --- delimiter
725 NEXT i
730 IF r$ <> "---" THEN GOTO 940
735 RETURN
900 PRINT "Bad Leading delimiter"
905 PRINT r$
910 END
920 PRINT "Bad Command Type - should be 1"
925 PRINT t
930 END
940 PRINT "Bad Trailing delimiter"
945 PRINT r$
950 END

