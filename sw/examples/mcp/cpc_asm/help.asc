10 MEMORY &6FFF: ' reserve 7000 and above
20 ' load the ASM library to &8000
30 LOAD "MCPLIB.BIN",&8000:CALL &8000:REM Init RSXes
40 ' initialise the CP-LINK interface (and reset FIFO queue)
50 |FIFORST
60 ' define the TextCommand to send to the pi
70 c$ = "HELP"
80 ' store command in buffer
90 gosub 200
100 e% = 0
110 ' send the memory buffer as a TextCommand
120 |SENDTXTCMD,&7000,len(c$),@e%
130 if e% > 0 then goto 300
140 ' get the response (into memory buffer but also print)
150 e% = 0: s% = 0
160 |GETTXTCMDREPLY,&7000,@s%,1,@e%
170 if e% > 0 then goto 300
180 INPUT "Enter command for help: ",a$: c$="HELP "+a$
190 goto 90
200 ' store command c$ in memory buffer
210 FOR i=1 to len(c$)
220   poke &6FFF+i,asc(mid$(c$,i,1))
230 NEXT i
240 return
300 ' print returned error code meaning
310 print "Error in call "
320 if e% = 1 then print "  Invalid Parameters"
330 if e% = 2 then print "  Unexpected Packet Type"
340 if e% = 3 then print "  Malformed Packet"
350 if e% > 3 then print "  Unknown Error"
360 print "Failed."
