10 MEMORY &6FFF: ' reserve 7000 and above
20 ' load the ASM library to &8000
30 LOAD "MCPLIB.BIN",&8000
40 ' initialise the CP-LINK interface (and reset FIFO queue)
50 CALL &8000
60 MODE 2
70 WINDOW #2,1,80,1,5 : CLS #2
80 WINDOW #0,1,80,8,24 : CLS #0
90 PRINT #2,"  CPC-CPlink/Pi MCP Terminal" 
100 PRINT #2: PRINT #2,"  Some commands:"  
110 PRINT #2,"  - HELP, TIME, DATE, SHUTDOWN"
120 MOVE 10,396:DRAW 2,396:DRAW 2,320:DRAW 638,320:DRAW 638,396:DRAW 232,396
130 c$ = ""
140 INPUT "MCP> ",c$
150 ' store command in buffer
160 gosub 300
170 e% = 0
180 ' send the memory buffer as a TextCommand
190 call &8003,&7000,len(c$),@e%
200 if e% > 0 then goto 400
210 ' get the response (into memory buffer but also print)
220 e% = 0: s% = 0
230 call &8006,&7000,@s%,1,@e%
240 if e% > 0 then goto 400
250 goto 140
300 ' store command c$ in memory buffer
310 FOR i=1 to len(c$)
320   poke &6FFF+i,asc(mid$(c$,i,1))
330 NEXT i
340 return
400 ' print returned error code meaning
410 print "Error in call "
420 if e% = 1 then print "  Invalid Parameters"
430 if e% = 2 then print "  Unexpected Packet Type"
440 if e% = 3 then print "  Malformed Packet"
450 if e% > 3 then print "  Unknown Error"
460 print "Failed."

